# ********************************************************************************************************************
#                                                      NimbleSM
#                                                VALIDATION TESTING
#
# ## Test files requirements ##
# 1/ NimbleSM executables shall be named NimbleSM_XXXX with XXXX being a specific version (Serial, Kokkos, ...)
# 2/ Convention over configuration: Each test shall be stored in a separate folder. The name of the folder and the
#    files it contains must be identical.
#    Example: test/example1/example1.in example1.g example1.gold.e example1.exodiff
# 3/ All tests are run using the default python interpreter in the current environment
# 4/ A typical test contains the following files:
#      .g       the model
#      .in      details of the simulation
#      .exodiff tolerances between gold.e file and generated .e file
#      .gold.e  "gold" file containing expected results
#      .g.x.y   Files generated by seacas `decomp` for parallel processing
#
# ## Configuring tests ##
# 1/ NimbleSM Binaries
# The variable `lstVersions` is a list that contains the suffix of all binaries to be tested.
# eg. If the build generates `NimbleSM_Serial` and `NimbleSM_Kokkos`, then lstVersions shall contain
#     two values: "Serial" and "Kokkos".
#
# 2/ Command lines for each binary
# For each binary, one or more command lines shall be specified. Such command lines are stored as string lists
# in variables named lstTestCLI_BINARY, with BINARY being one of the suffixes mentioned above.
# eg. list(APPEND lstTestsCLI_Kokkos
#        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --ranks 1"
#        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --ranks 2"
#        )
#
# Each command line will generate a new test. By default, the name of each test is suffixed by a number
# incremented (0,1,2...) for each different CLI.
# It is possible to OPTIONALLY specify strings of text as custom suffixes by adding an optional variable named
# the same as the above list, with _Suffixes added at the end.
#
# eg. list(APPEND lstTestsCLI_Kokkos_Suffixes
#        "np1"
#        "np2"
#        )
#
# 3/ Tests to be run
# For each binary, one or more tests shall be specified. The tests have to be stored according to the
# instructions above (## Test files requirements ##). The name of a test is the name of its folder.
# Test lists are stored as string lists in variable named lstTests_BINARY with BINARY being one of the
# suffixes mentioned above.
# eg. list(APPEND lstTests_Serial
#       test1
#       test2
#       )
#
# 4/ Multiple test lists per binary
# In some cases, for a given binary, certain tests shall only be run using specific command lines.
# For example, a test might be designed for Serial only, although a binary is being tested with two
# different command lines, one for Serial and one for Parallel calculations.
# In such case, additional CLI/Test lists might be provided for that given binary.
# eg. As an example, we can define two test lists for Kokkos, and arbitrarily define that:
#   lstTestsCLI_Kokkos_A --> Command lines for both Serial and Parallel executions
#   lstTestsCLI_Kokkos_A_Suffixes --> Suffixes for each test
#   lstTests_Kokkos_A --> Tests that can be run both in Serial and Parallel
#   lstTestsCLI_Kokkos_B --> Command line for Serial ONLY
#   lstTestsCLI_Kokkos_B_Suffixes --> Suffixes for each test
#   lstTests_Kokkos_B --> Tests that can be run in Serial ONLY
# The naming convention is the same as in 2/ and 3/, except that a suffix "_A", "_B" or "_C" is added
# to the variable names.

message(STATUS "### TESTS CONFIGURATION STARTS ###")

# Defines all versions of NimbleSM to be tested
IF(NIMBLE_HAVE_ARBORX)
    list(APPEND lstVersions
            ArborX
            )
ENDIF()

IF(NIMBLE_HAVE_TRILINOS)
    list(APPEND lstVersions
            Tpetra
            )
ENDIF()

list(APPEND lstVersions
     MPI
    )

IF(NIMBLE_HAVE_KOKKOS AND NOT NIMBLE_HAVE_ARBORX)
    list(APPEND lstVersions
            Kokkos
            )
ENDIF()

if(NIMBLE_HAVE_DARMA)
    list(APPEND lstVersions
            Darma
            )
endif()

if(NIMBLE_HAVE_QTHREADS)
    list(APPEND lstVersions
            Qthreads
            )
endif()


# Generate paths to executables
foreach(version IN LISTS lstVersions)
    set(${version}_Path ${PROJECT_BINARY_DIR}/src/NimbleSM_${version})
    message(STATUS "${version} version's path: ${${version}_Path}")
endforeach()

### Defines tests to be run for each version of NimbleSM ###

### ARBORX ###

# "A" Test list for tests that can work both in Serial and MPI modes
list(APPEND lstTestsCLI_ArborX_A
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1"
        )

# Optional *_Suffixes variable specifies how tests names should be suffixed. Each suffix
#  corresponds to on of the above command lines. It should have as many suffixes as there are
#  command lines for a given binary & test set (A, B, C).
list(APPEND lstTestsCLI_ArborX_A_Suffixes
        "np1"
        )


# Add multi-rank testing to Kokkos if MPI is enabled
IF(NIMBLE_HAVE_MPI)
    list(APPEND lstTestsCLI_ArborX_A
            "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 2"
            )
    list(APPEND lstTestsCLI_ArborX_A_Suffixes
            "np2"
            )
    list(APPEND lstTestsCLI_ArborX_A
            "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 4"
            )
    list(APPEND lstTestsCLI_ArborX_A_Suffixes
            "np4"
            )
ENDIF()

list(APPEND lstTests_ArborX_A
#        brick_with_fiber_periodic
#        brick_with_fibers
        contact_entity_creation
#       cylinder_with_hole
#        notched_plate_native_hypoelastic
#        notched_plate_native_neohookean
#        plate_with_hole_rve
#        quasistatic_tension
#        rigid_body_motion
#        simple_deformation_modes
#        single_elem_complex_displacement
        sphere_plate_contact
#        tiny_fe_squared
#        wave_in_bar
        )

# "B" test list for tests that can only work in Serial
list(APPEND lstTestsCLI_ArborX_B
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1"
        )

list(APPEND lstTestsCLI_ArborX_B_Suffixes
        "serial-only"
        )

list(APPEND lstTests_ArborX_B
#        single_elem_complex_displacement
        cubes_contact
        )


# "C" test list for tests running with 1, 2, 4, 8 processors
IF(NIMBLE_HAVE_MPI)
    list(APPEND lstTestsCLI_ArborX_C
            "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1"
            )
    list(APPEND lstTestsCLI_ArborX_C_Suffixes
            "np1"
            )
    list(APPEND lstTestsCLI_ArborX_C
            "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 2"
            )
    list(APPEND lstTestsCLI_ArborX_C_Suffixes
            "np2"
            )
    list(APPEND lstTestsCLI_ArborX_C
            "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 4"
            )
    list(APPEND lstTestsCLI_ArborX_C_Suffixes
            "np4"
            )
#    list(APPEND lstTestsCLI_ArborX_C
#            "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 8"
#            )
#    list(APPEND lstTestsCLI_ArborX_C_Suffixes
#            "np8"
#            )
    list(APPEND lstTests_ArborX_C
            sphere_plate_contact
            )
ENDIF()


### KOKKOS ###

# "A" Test list for tests that can work both in Serial and MPI modes
list(APPEND lstTestsCLI_Kokkos_A
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1"
        )

list(APPEND lstTestsCLI_Kokkos_A_Suffixes
        "np1"
        )

# Add multi-rank testing to Kokkos if MPI is enabled
IF(NIMBLE_HAVE_MPI)
    list(APPEND lstTestsCLI_Kokkos_A
            "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 2"
            )

    list(APPEND lstTestsCLI_Kokkos_A_Suffixes
            "np2"
            )
#   list(APPEND lstTestsCLI_Kokkos_A
#           "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 4"
#           )

#   list(APPEND lstTestsCLI_Kokkos_A_Suffixes
#           "np4"
#           )
ENDIF()

list(APPEND lstTests_Kokkos_A
        brick_with_fibers
        contact_entity_creation
#        cylinder_with_hole
        notched_plate_native_hypoelastic
        notched_plate_native_neohookean
#        plate_with_hole_rve
#        quasistatic_tension
        rigid_body_motion
        simple_deformation_modes
#        tiny_fe_squared
        wave_in_bar
        )

# "B" test list for tests that can only work in Serial
list(APPEND lstTestsCLI_Kokkos_B
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1"
        )

list(APPEND lstTestsCLI_Kokkos_B_Suffixes
        "np1"
        )

list(APPEND lstTests_Kokkos_B
        single_elem_complex_displacement
        )

### MPI ###

list(APPEND lstTestsCLI_MPI_A
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1"
     )

list(APPEND lstTestsCLI_MPI_A_Suffixes
        "np1"
    )

IF (NIMBLE_HAVE_MPI)
  list(APPEND lstTestsCLI_MPI_A
       "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 2"
       "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 4"
        )
  list(APPEND lstTestsCLI_MPI_A_Suffixes
       "np2"
       "np4"
       )
ENDIF()

list(APPEND lstTests_MPI_A
#        brick_with_fiber_periodic
        brick_with_fibers
#        contact_entity_creation
#        cylinder_with_hole
        notched_plate_native_hypoelastic
        notched_plate_native_neohookean
#        plate_with_hole_rve
#        quasistatic_tension
        rigid_body_motion
        simple_deformation_modes
#        tiny_fe_squared
        wave_in_bar
        )

list(APPEND lstTestsCLI_MPI_B
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1"
        )

list(APPEND lstTestsCLI_MPI_B_Suffixes
        "np1"
        )

list(APPEND lstTests_MPI_B
        single_elem_complex_displacement
        )

#######################################
#--- Add contact tests when BVH is used
if (NIMBLE_HAVE_BVH)
  list(APPEND lstTests_MPI_A
       contact_entity_creation
       sphere_plate_contact 
      )
  #
  list(APPEND lstTests_MPI_B
       cubes_contact
      )
  #
endif()
#######################################

### TRILINOS ###
# Same tests as MPI + brick_with_fiber_periodic

list(APPEND lstTestsCLI_Tpetra_A
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1"
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 2"
        )

list(APPEND lstTestsCLI_Tpetra_A_Suffixes
        "np1"
        "np2"
        )

list(APPEND lstTests_Tpetra_A
        ${lstTests_MPI_A}
        )

list(APPEND lstTestsCLI_Tpetra_B
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1"
        )

list(APPEND lstTestsCLI_Tpetra_B_Suffixes
        "np1"
        )

list(APPEND lstTests_Tpetra_B
        brick_with_fiber_periodic
        ${lstTests_MPI_B}
        )


### Darma ###
# Same tests as MPI

list(APPEND lstTestsCLI_Darma_A
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1 --num-virtual-ranks 1"
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 4 --num-virtual-ranks 8"
        )

list(APPEND lstTestsCLI_Darma_A_Suffixes
        "np1"
        "np2"
        )

list(APPEND lstTests_Darma_A
        ${lstTests_MPI_A}
        )

list(APPEND lstTestsCLI_Darma_B
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1 --num-virtual-ranks 1"
        )

list(APPEND lstTestsCLI_Darma_B_Suffixes
        "np1"
        )

list(APPEND lstTests_Darma_B
        ${lstTests_MPI_B}
        )


### Qthreads ###
# Same tests as MPI

list(APPEND lstTestsCLI_Qthreads_A
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1 --qthreads-num-shepherds 1 --qthreads-num-workers-per-shepherd 1"
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 2 --qthreads-num-shepherds 2 --qthreads-num-workers-per-shepherd 1"
        )

list(APPEND lstTestsCLI_Qthreads_A_Suffixes
        "np1"
        "np2"
        )

list(APPEND lstTestsCLI_Qthreads_A
        ${lstTests_MPI_A}
        )

list(APPEND lstTestsCLI_Qthreads_B
        "../run_exodiff_test.py --executable _EXECUTABLE_ --input-deck _INPUT_DECK_ --num-ranks 1 --qthreads-num-shepherds 1 --qthreads-num-workers-per-shepherd 1"
        )

list(APPEND lstTestsCLI_Qthreads_B_Suffixes
        "np1"
        )

list(APPEND lstTestsCLI_Qthreads_B
        ${lstTests_MPI_B}
        )


# Build list of available tests (appending tests selected for each version of NimbleSM and removing duplicates)
foreach (version IN LISTS lstVersions)
    foreach(listID in "" "_A" "_B" "_C")
        if(DEFINED lstTests_${version}${listID})
            list(APPEND lstTests
                    ${lstTests_${version}${listID}})
        endif()
    endforeach()
endforeach ()

list(REMOVE_DUPLICATES lstTests)

# File copy to binary dir
message(STATUS "### Copying all files necessary for testing to binary dir
     - run_exodiff_test.py
     - Files with extensions .in, .g, .gold.e, .exodiff
     - Files with extentions .g.*")

# Copy run_exodiff_test.py
configure_file(
        ${CMAKE_CURRENT_SOURCE_DIR}/run_exodiff_test.py
        ${CMAKE_CURRENT_BINARY_DIR}/run_exodiff_test.py
        COPYONLY)

# Copy all other files
foreach(test IN LISTS lstTests)
    message(STATUS " * ${test}")
    # basic extensions (in, g, gold.e, exodiff)
    foreach(ext in g gold.e exodiff)
        configure_file(
                ${CMAKE_CURRENT_SOURCE_DIR}/${test}/${test}.${ext}
                ${CMAKE_CURRENT_BINARY_DIR}/${test}/${test}.${ext}
                COPYONLY)
    endforeach()

    # files generated by seacas `decomp` for multiple-rank environments
    file(GLOB files ${CMAKE_CURRENT_SOURCE_DIR}/${test}/${test}.g.*)
    foreach(file ${files})
        get_filename_component(filename ${file} NAME)
        configure_file(
                ${file}
                ${CMAKE_CURRENT_BINARY_DIR}/${test}/${filename}
                COPYONLY)
    endforeach()
endforeach()


###    MAIN LOOP    ###
# Generates all tests #
#######################

message(STATUS "## Adding tests to CMAKE: ##")
# Go through each version of NimbleSM
foreach(version IN LISTS lstVersions)
    message(STATUS " * Version: ${version}")

    # Go through each test list
    foreach(listID in "" "_A" "_B" "_C")
        # For each version, go through each test to run
        foreach(test IN LISTS lstTests_${version}${listID})
            if(DEFINED lstTestsCLI_${version}${listID})
                set(lstCLI lstTestsCLI_${version}${listID})

                # Determines the number of Command Line variants to run for that version of NimbleSM
                list(LENGTH ${lstCLI} count)
                math(EXPR count "${count}-1")

                # For each test, go through each Command Line variant
                foreach(i RANGE ${count})
                    # Set the Command Line, test name and working directory for the current test
                    list(GET ${lstCLI} ${i} cli)

                    # Check if a specific suffix was defined. If not, suffix will be set to ${i}
                    if(DEFINED lstTestsCLI_${version}${listID}_Suffixes)
                        list(GET lstTestsCLI_${version}${listID}_Suffixes ${i} suffix )
                    else()
                        set(suffix ${i})
                    endif()

                    set(test_name ${test}_${version}${listID}_${suffix})
                    set(working_dir ${CMAKE_CURRENT_BINARY_DIR}/${test})

                    # Expand the command line by replacing "_EXECUTABLE_" and "_INPUT_DECK_" with variables
                    string(REPLACE _EXECUTABLE_
                    ${${version}_Path} cli
                    ${cli}
                    )

                    string(REPLACE _INPUT_DECK_
                    ${test}.in cli
                    ${cli}
                    )

                    message(STATUS "  - ${test_name}: python ${cli} (work.dir: ${working_dir})")

                    # Change cli from string to list (list required by add_test)
                    separate_arguments(cli)

                    # Add the test
                    add_test(NAME ${test_name}
                            COMMAND python ${cli}
                            WORKING_DIRECTORY ${working_dir}
                            )

                endforeach() # CLI variant
            endif()
        endforeach() # Test
    endforeach() # Test list
endforeach() # NimbleSM Version

message(STATUS "### TESTS CONFIGURATION COMPLETED ###")
